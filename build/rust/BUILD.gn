import("//huhi/build/cargo.gni")

lib_build_path = "$root_build_dir/huhi_rust/out/$current_cpu"
lib_output_path = "$lib_build_path/${cargo_lib_prefix}huhi_rust${cargo_lib_suffix}"

# TODO(bridiver) - selectively include these libraries based on buildflags, but
# in order to do that we have to dynamically create the huhi rust lib
cargo_build("rust_lib") {
  visibility = [ ":*" ]
  inputs = [
    "Cargo.toml",
    "lib.rs",
  ]

  build_path = lib_build_path
  output = lib_output_path
  manifest_path = "Cargo.toml"

  deps = [
    "//huhi/components/challenge_bypass_ristretto:rust_lib",
    "//huhi/components/speedreader/rust/lib",
    "//huhi/vendor/adblock_rust_ffi:rust_lib",
  ]
}

huhi_ffi_deps = [
  "//huhi/components/challenge_bypass_ristretto:rust_ffi",
  "//huhi/components/speedreader/rust/ffi",
  "//huhi/vendor/adblock_rust_ffi:rust_ffi",
]

huhi_rust_deps = huhi_ffi_deps + [ ":rust_lib" ]

if (is_mac) {
  shared_library("huhi_rust") {
    visibility = [ ":*" ]
    deps = huhi_rust_deps

    public_deps = huhi_ffi_deps

    ldflags = [
      "-Wl,-install_name,@loader_path/Libraries/lib$target_name.dylib",
    ]
  }

  # This is just a hack so @loader_path/Libraries/lib$target_name.dylib
  # works for the test executables
  copy("copy_rust_lib") {
    visibility = [ ":*" ]
    sources = [
      "$root_out_dir/libhuhi_rust.dylib",
    ]
    outputs = [ "$root_out_dir/Libraries/{{source_file_part}}" ]

    public_deps = [ ":huhi_rust" ]
  }

  bundle_data("bundle_rust_lib") {
    visibility = [ ":*" ]
    sources = [
      "$root_out_dir/Libraries/libhuhi_rust.dylib",
    ]
    outputs = [ "{{bundle_contents_dir}}/Libraries/{{source_file_part}}" ]

    public_deps = [
      ":copy_rust_lib"
    ]
  }
} else {
  config("external_config") {
    libs = [ lib_output_path ]
  }

  group("huhi_rust") {
    public_configs = [ ":external_config" ]
    visibility = [ ":*" ]
    deps = huhi_rust_deps
    public_deps = huhi_ffi_deps
  }
}

group("rust") {
  public_deps = [ ":huhi_rust" ]
  if (is_mac) {
    deps = [
      ":bundle_rust_lib"
    ]
  }
}
