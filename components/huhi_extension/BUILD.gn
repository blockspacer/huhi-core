import("//huhi/components/common/typescript.gni")
import("//build/config/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

assert(enable_extensions)

pack_web_resources("generated_resources") {
  resource_name = "huhi_extension"
  output_dir = "$root_gen_dir/huhi/components/huhi_extension"
  # need wepack and GRD generation first
  deps = [
    "extension/huhi_extension"
  ]
}

grit("static_resources") {
  source = "extension/resources.grd"

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  outputs = [
    "grit/huhi_extension.h",
    "grit/huhi_extension_resources_map.cc",
    "grit/huhi_extension_resources_map.h",
    "huhi_extension_static.pak",
  ]

  deps = [
    "extension/huhi_extension",
  ]

  resource_ids = "//huhi/browser/resources/resource_ids"
  output_dir = "$root_gen_dir/huhi/components/huhi_extension"
}

repack("resources") {
  deps = [
    ":static_resources",
    ":generated_resources",
  ]

  sources = [
    "$root_gen_dir/huhi/components/huhi_extension/huhi_extension_static.pak",
    "$root_gen_dir/huhi/components/huhi_extension/huhi_extension_generated.pak",
  ]

  output = "$root_gen_dir/huhi/components/huhi_extension/huhi_extension_resources.pak"
}
