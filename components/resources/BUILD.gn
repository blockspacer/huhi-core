import("//huhi/components/huhi_perf_predictor/browser/buildflags/buildflags.gni")
import("//huhi/components/huhi_rewards/browser/buildflags/buildflags.gni")
import("//huhi/components/huhi_wallet/browser/buildflags/buildflags.gni")
import("//huhi/components/ipfs/browser/buildflags/buildflags.gni")
import("//huhi/components/moonpay/browser/buildflags/buildflags.gni")
import("//huhi/components/speedreader/buildflags.gni")
import("//build/config/locales.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

grit("static_resources") {
  source = "huhi_components_resources.grd"

  output_name = "huhi_components_resources_new"

  outputs = [
    "grit/huhi_components_resources.h",
    "huhi_components_static.pak",
  ]
  deps = [
    ":strings",
  ]

  if (enable_extensions) {
    deps += [
      "//huhi/components/huhi_extension/extension/huhi_extension",
    ]
  }
  if (huhi_rewards_enabled) {
    deps += [
      "//huhi/components/huhi_rewards/resources",
    ]
    if (enable_extensions) {
      deps += [
        "//huhi/components/huhi_rewards/resources/extension:resources",
      ]
    }
  }

  defines = [
    "enable_huhi_perf_predictor=$enable_huhi_perf_predictor",
    "enable_speedreader=$enable_speedreader",
    "ipfs_enabled=$ipfs_enabled",
    "moonpay_enabled=$moonpay_enabled",
  ]

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  output_dir = "$root_gen_dir/components"
  resource_ids = "//huhi/browser/resources/resource_ids"
}

repack("resources") {
  deps = [
    ":static_resources",
    "//huhi/components/huhi_adblock_ui:generated_resources",
    "//huhi/components/webcompat_reporter/ui:generated_resources",
    "//huhi/components/huhi_new_tab_ui:generated_resources",
    "//huhi/components/huhi_welcome_ui:generated_resources",
  ]

  sources = [
    "$root_gen_dir/components/huhi_components_static.pak",
    "$root_gen_dir/huhi/components/huhi_adblock/resources/huhi_adblock_generated.pak",
    "$root_gen_dir/huhi/components/webcompat_reporter/resources/webcompat_reporter_generated.pak",
    "$root_gen_dir/huhi/components/huhi_new_tab/resources/huhi_new_tab_generated.pak",
    "$root_gen_dir/huhi/components/huhi_welcome/resources/huhi_welcome_generated.pak",
  ]

  if (ipfs_enabled) {
    deps += [
      "//huhi/components/ipfs_ui:generated_resources",
    ]
    sources += [
      "$root_gen_dir/huhi/components/ipfs_ui/resources/ipfs_generated.pak",
    ]
  }

  if (huhi_wallet_enabled) {
    deps += [
      "//huhi/components/huhi_wallet_ui:generated_resources",
    ]
    sources += [
      "$root_gen_dir/huhi/components/huhi_wallet/resources/huhi_wallet_generated.pak",
    ]
  }

  output = "$root_gen_dir/components/huhi_components_resources.pak"
}

grit("strings") {
  source = "huhi_components_strings.grd"

  outputs = [
    "grit/huhi_components_strings.h",
  ]

  defines = [
    "enable_speedreader=$enable_speedreader",
    "ipfs_enabled=$ipfs_enabled",
    "moonpay_enabled=$moonpay_enabled",
  ]

  foreach(locale, locales_with_fake_bidi) {
    outputs += [ "huhi_components_strings_$locale.pak" ]
  }

  output_dir = "$root_gen_dir/components"
  resource_ids = "//huhi/browser/resources/resource_ids"
}

action("about_credits") {
  script = "//huhi/script/check_npm_licenses.py"

  inputs = [ "//huhi/package.json" ]

  outputs = [ "$root_gen_dir/npm_licenses.checked" ]

  args = [
    rebase_path("$root_gen_dir/npm_licenses.checked")
  ]
}
