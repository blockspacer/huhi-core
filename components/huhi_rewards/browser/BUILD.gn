import("//huhi/components/huhi_rewards/browser/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browser") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10618 is resolved
  check_includes = false
  public_deps = [
    "buildflags",
  ]

  sources = [
    "android_util.cc",
    "android_util.h",
    "switches.cc",
    "switches.h",
    "rewards_notification_service.cc",
    "rewards_notification_service.h",
    "rewards_notification_service_observer.h",
    "rewards_p3a.cc",
    "rewards_p3a.h",
    "rewards_service.cc",
    "rewards_service.h",
    "rewards_service_observer.h",
    "file_util.cc",
    "file_util.h",
    "logging_util.cc",
    "logging_util.h",
    "logging.h",
    "rewards_protocol_handler.h",
    "rewards_protocol_handler.cc",
    "static_values.h",
  ]

  deps = [
    "//base",
    "//huhi/base",
    "//huhi/components/huhi_rewards/common",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/sessions",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
  ]

  if (!is_android) {
    # on android, one shot is part of base lib
    deps += [
      # for one shot event
      "//extensions/common",
    ]
  }

  if (huhi_rewards_enabled) {
    sources += [
      "net/network_delegate_helper.cc",
      "net/network_delegate_helper.h",
      "rewards_service_impl.cc",
      "rewards_service_impl.h",
      "rewards_notification_service_impl.cc",
      "rewards_notification_service_impl.h",
    ]

    deps += [
      "//huhi/components/huhi_ads/browser/buildflags",
      "//huhi/components/resources",
      "//huhi/components/services/bat_ledger/public/cpp",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//url",
    ]

    public_deps += [
      "//huhi/vendor/bat-native-ledger:headers",
    ]

    if (is_android) {
      deps += [ "//huhi/components/safetynet:android" ]
    }
  }
}

source_set("testutil") {
  testonly = true
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10618 is resolved
  check_includes = false

  deps = [
    "//huhi/vendor/bat-native-ledger:headers",
    ":browser",
    "//base",
    "//content/public/browser",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]

  sources = [
    "test_util.cc",
    "test_util.h",
  ]
}
