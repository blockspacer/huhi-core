# Copyright (c) 2020 The Huhi Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//huhi/build/config.gni")
import("//huhi/components/huhi_wayback_machine/buildflags/buildflags.gni")
import("//huhi/components/tor/buildflags/buildflags.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//chrome/browser/resources/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/polymer/polymer.gni")

grit("resources") {
  source = "settings_resources.grd"

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  deps = [ ":web_modules" ]
  defines = [
    "is_release_channel=$is_release_channel",
    "enable_tor=$enable_tor",
    "enable_huhi_wayback_machine=$enable_huhi_wayback_machine",
  ]

  enable_input_discovery_for_gn_analyze = optimize_webui

  outputs = [
    "grit/huhi_settings_resources.h",
    "grit/huhi_settings_resources_map.cc",
    "grit/huhi_settings_resources_map.h",
    "huhi_settings_resources.pak",
  ]

  output_dir = "$root_gen_dir/huhi/browser/resources/settings"
  resource_ids = "//huhi/browser/resources/resource_ids"
}

if (optimize_webui) {
  group("unpak") {
    deps = [
      ":unpak_huhi_settings_resources",
    ]
  }

  action("unpak_huhi_settings_resources") {
    script = "//chrome/browser/resources/unpack_pak.py"

    pak_file = "huhi_settings_resources.pak"
    out_folder = "$root_gen_dir/chrome/browser/resources/settings/preprocessed"

    inputs = [
      "$target_gen_dir/huhi_settings_resources.pak",
    ]

    outputs = [
      "${out_folder}/huhi_unpack.stamp",
    ]

    deps = [
      ":resources",
    ]

    args = [
      "--out_folder",
      rebase_path(out_folder, root_build_dir),
      "--pak_file",
      rebase_path("$target_gen_dir/${pak_file}", root_build_dir),
    ]
  }
}

group("web_modules") {
  public_deps = [
    # Sub-folder targets
    "huhi_appearance_page:web_modules",
    "huhi_clear_browsing_data_dialog:web_modules",
    "huhi_default_extensions_page:web_modules",
    "huhi_help_tips_page:web_modules",
    "huhi_new_tab_page:web_modules",
    "huhi_privacy_page:web_modules",
    "huhi_sync_page:web_modules",
    "default_huhi_shields_page:web_modules",
    "getting_started_page:web_modules",
    "social_blocking_page:web_modules",
    # local targets
    ":icons_module",
  ]
}

polymer_modulizer("icons") {
  js_file = "huhi_icons.m.js"
  html_file = "huhi_icons.html"
  html_type = "iron-iconset"
}
