import("//huhi/components/tor/buildflags/buildflags.gni")

source_set("tor") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10655 is resolved
  check_includes = false
  visibility = [
    "//huhi/browser:browser_process",
    "//huhi/browser/profiles",
    "//huhi/browser/ui",
    "//huhi/test:*",
    ":*",
  ]

  if (enable_tor) {
    sources = [
      "onion_location_navigation_throttle_delegate.cc",
      "onion_location_navigation_throttle_delegate.h",
      "tor_profile_service_factory.cc",
      "tor_profile_service_factory.h",
    ]

    deps = [
      "//base",
      "//huhi/components/tor",
      "//huhi/components/tor:pref_names",
      "//chrome/common",
      "//components/keyed_service/content",
      "//content/public/browser",
    ]

    # Below dep list are not directly used tor target.
    # Only added to fix intermittent build failure.
    # Most of the case comes from including huhi_browser_process_impl.h`
    # headers. That header includes some buildflags but sometimes they are
    # not generated before evaluating tor target.
    deps += [
      "//huhi/components/huhi_referrals/buildflags",
      "//huhi/components/greaselion/browser/buildflags",
      "//huhi/components/speedreader:buildflags",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  if (enable_tor) {
    sources = [ "tor_navigation_throttle_unittest.cc" ]

    deps = [
      "//base",
      "//base/test:test_support",
      "//huhi/browser/profiles",
      "//huhi/browser/tor",
      "//huhi/components/tor",
      "//huhi/test:test_support",
      "//chrome/test:test_support",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
      "//url",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "huhi_local_state_browsertest.cc",
      "huhi_tor_client_updater_browsertest.cc",
      "onion_location_navigation_throttle_browsertest.cc",
    ]

    deps = [
      "//base",
      "//huhi/app:huhi_generated_resources_grit",
      "//huhi/browser:browser_process",
      "//huhi/browser/profiles",
      "//huhi/browser/tor",
      "//huhi/browser/ui",
      "//huhi/common",
      "//huhi/components/tor",
      "//huhi/components/tor:pref_names",
      "//chrome/browser",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//content/public/browser",
      "//content/test:test_support",
      "//net:test_support",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}
