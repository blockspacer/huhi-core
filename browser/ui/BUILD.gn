import("//huhi/browser/translate/buildflags/buildflags.gni")
import("//huhi/build/config.gni")
import("//huhi/components/huhi_rewards/browser/buildflags/buildflags.gni")
import("//huhi/components/huhi_wallet/buildflags/buildflags.gni")
import("//huhi/components/huhi_wayback_machine/buildflags/buildflags.gni")
import("//huhi/components/ipfs/buildflags/buildflags.gni")
import("//huhi/components/speedreader/buildflags.gni")
import("//huhi/components/tor/buildflags/buildflags.gni")
import("//build/config/features.gni")
import("//components/gcm_driver/config.gni")
import("//chrome/common/features.gni")

source_set("favicon_source") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10614 is resolved
  check_includes = false
  sources = [
    "//chrome/browser/ui/webui/favicon_source.cc",
    "//chrome/browser/ui/webui/favicon_source.h",
  ]

  deps = [
    "//components/favicon/core",
    "//components/favicon_base",
    "//components/history/core/browser",
    "//net",
    "//ui/native_theme",
    "//url",
  ]
}

source_set("ui") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10614 is resolved
  check_includes = false
  sources = [
    "webui/basic_ui.cc",
    "webui/basic_ui.h",
    "webui/huhi_adblock_ui.cc",
    "webui/huhi_adblock_ui.h",
    "webui/webcompat_reporter_ui.cc",
    "webui/webcompat_reporter_ui.h",
    "webui/huhi_web_ui_controller_factory.cc",
    "webui/huhi_web_ui_controller_factory.h",
    "webui/huhi_webui_source.cc",
    "webui/huhi_webui_source.h",
  ]

  if (ipfs_enabled) {
    sources += [
      "webui/ipfs_ui.cc",
      "webui/ipfs_ui.h",
    ]
  }

  if (!is_android) {
    sources += [
      "huhi_browser_command_controller.cc",
      "huhi_browser_command_controller.h",
      "huhi_browser_content_setting_bubble_model_delegate.cc",
      "huhi_browser_content_setting_bubble_model_delegate.h",
      "huhi_browser_window.h",
      "huhi_layout_constants.cc",
      "huhi_layout_constants.h",
      "huhi_pages.cc",
      "huhi_pages.h",
      "huhi_view_ids.h",
      "browser_commands.cc",
      "browser_commands.h",
      "content_settings/huhi_autoplay_blocked_image_model.cc",
      "content_settings/huhi_autoplay_blocked_image_model.h",
      "content_settings/huhi_autoplay_content_setting_bubble_model.cc",
      "content_settings/huhi_autoplay_content_setting_bubble_model.h",
      "content_settings/huhi_content_setting_bubble_model.cc",
      "content_settings/huhi_content_setting_bubble_model.h",
      "content_settings/huhi_content_setting_image_models.cc",
      "content_settings/huhi_content_setting_image_models.h",
      "omnibox/huhi_omnibox_client_impl.cc",
      "omnibox/huhi_omnibox_client_impl.h",
      "toolbar/huhi_app_menu_model.cc",
      "toolbar/huhi_app_menu_model.h",
      "webui/huhi_settings_ui.cc",
      "webui/huhi_settings_ui.h",
      "webui/huhi_welcome_ui.cc",
      "webui/huhi_welcome_ui.h",
      "webui/navigation_bar_data_provider.cc",
      "webui/navigation_bar_data_provider.h",
      "webui/new_tab_page/huhi_new_tab_message_handler.cc",
      "webui/new_tab_page/huhi_new_tab_message_handler.h",
      "webui/new_tab_page/huhi_new_tab_ui.cc",
      "webui/new_tab_page/huhi_new_tab_ui.h",
      "webui/new_tab_page/instant_service_message_handler.cc",
      "webui/new_tab_page/instant_service_message_handler.h",
      "webui/settings/huhi_appearance_handler.cc",
      "webui/settings/huhi_appearance_handler.h",
      "webui/settings/huhi_import_data_handler.h",
      "webui/settings/huhi_privacy_handler.cc",
      "webui/settings/huhi_privacy_handler.h",
      "webui/settings/huhi_sync_handler.cc",
      "webui/settings/huhi_sync_handler.h",
      "webui/settings/default_huhi_shields_handler.cc",
      "webui/settings/default_huhi_shields_handler.h",
    ]

    if (is_mac) {
      sources += [
        "webui/settings/huhi_import_data_handler_mac.mm",
      ]
    } else {
      sources += [
        "webui/settings/huhi_import_data_handler.cc",
      ]
    }

    if (enable_sparkle) {
      sources += [
        "webui/settings/huhi_relaunch_handler_mac.h",
        "webui/settings/huhi_relaunch_handler_mac.mm",
      ]
    }
  }

  if (toolkit_views) {
    sources += [
      "views/bookmarks/bookmark_bar_instructions_view.cc",
      "views/bookmarks/bookmark_bar_instructions_view.h",
      "views/huhi_layout_provider.cc",
      "views/huhi_layout_provider.h",
      "views/download/huhi_download_item_view.cc",
      "views/download/huhi_download_item_view.h",
      "views/frame/huhi_browser_view.cc",
      "views/frame/huhi_browser_view.h",
      "views/reader_mode/huhi_reader_mode_icon_view.cc",
      "views/reader_mode/huhi_reader_mode_icon_view.h",
      "views/rounded_separator.cc",
      "views/rounded_separator.h",
      "views/tabs/huhi_browser_tab_strip_controller.cc",
      "views/tabs/huhi_browser_tab_strip_controller.h",
      "views/tabs/huhi_tab_context_menu_contents.cc",
      "views/tabs/huhi_tab_context_menu_contents.h",
      "views/tabs/huhi_tab_strip.cc",
      "views/tabs/huhi_tab_strip.h",
      "views/toolbar/bookmark_button.cc",
      "views/toolbar/bookmark_button.h",
    ]

    if (enable_sparkle) {
      sources += [
        "views/update_recommended_message_box_mac.h",
        "views/update_recommended_message_box_mac.mm",
      ]
    }

    if (enable_huhi_translate_extension) {
      sources += [
        "views/translate/huhi_translate_bubble_view.cc",
        "views/translate/huhi_translate_bubble_view.h",
        "views/translate/huhi_translate_icon_view.cc",
        "views/translate/huhi_translate_icon_view.h",
      ]
    }
  }

  if (is_win || is_mac || is_desktop_linux) {
    sources += [
      "views/profiles/huhi_incognito_menu_view.cc",
      "views/profiles/huhi_incognito_menu_view.h",
      "views/profiles/huhi_profile_menu_view.cc",
      "views/profiles/huhi_profile_menu_view.h",
    ]
  }

  if (is_win || is_mac) {
    sources += [
      "views/frame/huhi_browser_frame.cc",
      "views/frame/huhi_browser_frame.h",
    ]
  }

  if (is_linux) {
    sources += [
      "views/huhi_views_delegate_linux.cc",
      "views/huhi_views_delegate_linux.h",
    ]
  }

  deps = [
    "tabs",
    "//base",
    "//huhi/app:command_ids",
    "//huhi/app/theme:huhi_theme_resources",
    "//huhi/app/theme:huhi_unscaled_resources",
    "//huhi/app/vector_icons:vector_icons",
    "//huhi/browser:sparkle_buildflags",
    "//huhi/browser/devtools",
    "//huhi/browser/gcm_driver",
    "//huhi/browser/profiles",
    "//huhi/browser/tor",
    # //chrome/browser/ui depends on //huhi/browser/ui, add this target here
    # to pull in dependencies needed for the overwrite codes in chromium_src.
    "//huhi/chromium_src/chrome/browser/ui",
    "//huhi/common",
    "//huhi/common:pref_names",
    "//huhi/components/huhi_adblock_ui:generated_resources",
    "//huhi/components/webcompat_reporter/ui:generated_resources",
    "//huhi/components/huhi_ads/browser",
    "//huhi/components/huhi_rewards/browser",
    "//huhi/components/huhi_rewards/resources",
    "//huhi/components/huhi_shields/browser",
    "//huhi/components/huhi_wallet/buildflags:buildflags",
    "//huhi/components/huhi_wayback_machine:buildflags",
    "//huhi/components/l10n/browser",
    "//huhi/components/l10n/common",
    "//huhi/components/crypto_dot_com/browser/buildflags:buildflags",
    "//huhi/components/moonpay/browser/buildflags:buildflags",
    "//huhi/components/ntp_background_images/browser",
    "//huhi/components/ntp_background_images/common",
    "//huhi/components/ntp_widget_utils/browser",
    "//huhi/components/p3a:buildflags",
    "//huhi/components/tor:pref_names",
    "//huhi/components/vector_icons",
    "//huhi/components/webcompat_reporter/browser",
    "//huhi/components/weekly_storage",
    "//chrome/app:command_ids",
    "//chrome/app/vector_icons:vector_icons",
    "//chrome/common",
    "//chrome/services/qrcode_generator",
    "//components/content_settings/browser",
    "//components/gcm_driver:gcm_buildflags",
    "//components/prefs",
    "//components/sessions",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/cpp",
    "//skia",
    "//ui/accessibility",
    "//ui/base",
    "//ui/gfx",
    "//ui/resources",
    "//url",
  ]

  # This is no longer compiled into Chromium on Android, but we still
  # need it
  if (is_android) {
    deps += [
      ":favicon_source",
    ]
  }

  if (enable_huhi_wayback_machine) {
    deps += [ "//huhi/components/huhi_wayback_machine" ]

    if (toolkit_views) {
      deps += [ "//huhi/browser/ui/views/infobars:huhi_wayback_machine" ]
    }
  }

  if (ipfs_enabled) {
    deps += [
      "//huhi/components/ipfs",
      "//huhi/components/ipfs_ui:generated_resources",
    ]
  }

  if (!is_android) {
    deps += [
      "//huhi/app:huhi_generated_resources_grit",
      "//huhi/browser:version_info",
      "//huhi/browser/resources/settings:resources",
      "//huhi/browser/ui/bookmark",
      "//huhi/components/huhi_sync",
      "//huhi/components/huhi_sync:profile_sync_service_helper",
      "//huhi/components/huhi_new_tab_ui:generated_resources",
      "//huhi/components/huhi_welcome_ui:generated_resources",
      "//components/sync_device_info",
      "//third_party/blink/public/common",
    ]
  }

  if (enable_extensions && toolkit_views) {
    sources += [
      "huhi_actions/huhi_action_icon_with_badge_image_source.cc",
      "huhi_actions/huhi_action_icon_with_badge_image_source.h",
      "huhi_actions/huhi_action_view_controller.cc",
      "huhi_actions/huhi_action_view_controller.h",
      "toolbar/huhi_location_bar_model_delegate.cc",
      "toolbar/huhi_location_bar_model_delegate.h",
      "toolbar/huhi_toolbar_actions_model.cc",
      "toolbar/huhi_toolbar_actions_model.h",
      "views/huhi_actions/huhi_action_view.cc",
      "views/huhi_actions/huhi_action_view.h",
      "views/huhi_actions/huhi_actions_container.cc",
      "views/huhi_actions/huhi_actions_container.h",
      "views/location_bar/huhi_location_bar_view.cc",
      "views/location_bar/huhi_location_bar_view.h",
      "views/location_bar/huhi_star_view.cc",
      "views/location_bar/huhi_star_view.h",
      "views/profiles/huhi_avatar_toolbar_button.cc",
      "views/profiles/huhi_avatar_toolbar_button.h",
      "views/tabs/huhi_alert_indicator.cc",
      "views/tabs/huhi_alert_indicator.h",
      "views/tabs/huhi_new_tab_button.cc",
      "views/tabs/huhi_new_tab_button.h",
      "views/toolbar/huhi_toolbar_view.cc",
      "views/toolbar/huhi_toolbar_view.h",
    ]

    if (enable_extensions) {
      sources += [
        "webui/settings/huhi_default_extensions_handler.cc",
        "webui/settings/huhi_default_extensions_handler.h",
      ]
    }

    if (huhi_rewards_enabled) {
      sources += [
        "views/huhi_actions/huhi_rewards_action_stub_view.cc",
        "views/huhi_actions/huhi_rewards_action_stub_view.h",
      ]
    }

    if (enable_tor) {
      sources += [
        "views/location_bar/onion_location_view.cc",
        "views/location_bar/onion_location_view.h",
      ]
    }

    deps += [
      "//huhi/browser/resources/extensions:resources",
      "//huhi/components/huhi_extension:generated_resources",
      "//huhi/components/huhi_extension:static_resources",
      "//huhi/browser/extensions",
      "//chrome/browser/extensions",
      "//components/sessions",
      "//extensions/browser",
      "//extensions/common:common_constants",
    ]
  }

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/ui/webui/help/version_updater_win.cc",
      "//chrome/browser/ui/webui/help/version_updater_win.h",
    ]
    deps += [ "//google_update" ]
  }

  if (huhi_rewards_enabled) {
    sources += [
      "webui/huhi_rewards_internals_ui.cc",
      "webui/huhi_rewards_internals_ui.h",
      "webui/huhi_rewards_source.cc",
      "webui/huhi_rewards_source.h",
      "webui/huhi_rewards_page_ui.cc",
      "webui/huhi_rewards_page_ui.h",
    ]

    if (!is_android) {
      sources += [
        "webui/huhi_tip_ui.cc",
        "webui/huhi_tip_ui.h",
      ]
    }

    deps += [
      "//huhi/components/huhi_rewards/resources:page_generated_resources",
      "//huhi/components/huhi_rewards/resources:internals_generated_resources",
      "//huhi/components/huhi_rewards/resources:tip_generated_resources",
      "//huhi/vendor/bat-native-ledger:headers",
    ]
  }

  if (huhi_wallet_enabled) {
    sources += [
      "webui/huhi_wallet_ui.cc",
      "webui/huhi_wallet_ui.h",
    ]
    deps += [ "//huhi/components/huhi_wallet_ui:generated_resources" ]
  }

  if (toolkit_views) {
    deps += [
      "//ui/events",
      "//ui/views",
    ]

    if (enable_huhi_translate_extension) {
      deps += [ "//components/translate/core/browser" ]
    }

    if (enable_speedreader) {
      sources += [
        "views/toolbar/speedreader_button.cc",
        "views/toolbar/speedreader_button.h",
      ]
      deps += [
        "//huhi/components/speedreader",
        "//huhi/components/speedreader:buildflags",
      ]
    }
  }
}
