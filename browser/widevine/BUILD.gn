import("//third_party/widevine/cdm/widevine.gni")

assert(bundle_widevine_cdm || enable_widevine_cdm_component)

source_set("widevine") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10644 is resolved
  check_includes = false
  deps = [
    "//base",
    "//huhi/app:huhi_generated_resources_grit",
    "//huhi/common:pref_names",
    "//chrome/browser/ui",
    "//chrome/common",
    "//components/content_settings/core/common",
    "//components/permissions",
    "//components/subresource_filter/content/browser",
    "//components/vector_icons",
    "//content/public/browser",
    "//content/public/common",
    "//third_party/widevine/cdm:buildflags",
    "//ui/base",
    "//url",
  ]

  sources = [
    "widevine_permission_request.cc",
    "widevine_permission_request.h",
    "widevine_utils.cc",
    "widevine_utils.h",
  ]

  if (bundle_widevine_cdm) {
    sources += [
      "huhi_widevine_bundle_manager.cc",
      "huhi_widevine_bundle_manager.h",
      "huhi_widevine_bundle_unzipper.cc",
      "huhi_widevine_bundle_unzipper.h",
    ]

    deps += [
      "//huhi/common",
      "//huhi/common:switches",
      "//components/prefs",
      "//components/pref_registry",
      "//components/services/unzip/content",
      "//components/services/unzip/public/cpp",
      "//content/public/browser",
      "//media/cdm:cdm_paths",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//third_party/widevine/cdm:headers",
    ]
  }
}
