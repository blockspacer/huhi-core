import("//huhi/build/config.gni")
import("//huhi/components/binance/browser/buildflags/buildflags.gni")
import("//huhi/components/huhi_ads/browser/buildflags/buildflags.gni")
import(
    "//huhi/components/huhi_perf_predictor/browser/buildflags/buildflags.gni")
import("//huhi/components/huhi_referrals/buildflags/buildflags.gni")
import("//huhi/components/huhi_sync/buildflags/buildflags.gni")
import("//huhi/components/huhi_together/buildflags/buildflags.gni")
import("//huhi/components/huhi_wallet/buildflags/buildflags.gni")
import("//huhi/components/huhi_wayback_machine/buildflags/buildflags.gni")
import("//huhi/components/huhi_webtorrent/browser/buildflags/buildflags.gni")
import("//huhi/components/crypto_dot_com/browser/buildflags/buildflags.gni")
import("//huhi/components/gemini/browser/buildflags/buildflags.gni")
import("//huhi/components/greaselion/browser/buildflags/buildflags.gni")
import("//huhi/components/ipfs/buildflags/buildflags.gni")
import("//huhi/components/moonpay/browser/buildflags/buildflags.gni")
import("//huhi/components/speedreader/buildflags.gni")
import("//huhi/components/tor/buildflags/buildflags.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")
import("//components/gcm_driver/config.gni")
import("//extensions/buildflags/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")
import("//third_party/widevine/cdm/widevine.gni")

source_set("browser_process") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10657 is resolved
  check_includes = false
  visibility = [
    "//chrome/browser/*",
    "//huhi/browser/*",
    "//huhi:browser_dependencies",
    "//huhi/components/huhi_ads/test:*",
    "//huhi/components/huhi_rewards/browser:testutil",
    "//huhi/components/huhi_rewards/test:*",
    "//huhi/components/ipfs/test:*",
    "//huhi/test:*",
  ]

  sources = [
    "autocomplete/huhi_autocomplete_scheme_classifier.cc",
    "autocomplete/huhi_autocomplete_scheme_classifier.h",
    "huhi_browser_main_extra_parts.cc",
    "huhi_browser_main_extra_parts.h",
    "huhi_browser_main_parts.cc",
    "huhi_browser_main_parts.h",
    "huhi_browser_process_impl.cc",
    "huhi_browser_process_impl.h",
    "huhi_content_browser_client.cc",
    "huhi_content_browser_client.h",
    "huhi_local_state_prefs.cc",
    "huhi_local_state_prefs.h",
    "huhi_profile_prefs.cc",
    "huhi_profile_prefs.h",
    "huhi_shields/ad_block_pref_service_factory.cc",
    "huhi_shields/ad_block_pref_service_factory.h",
    "huhi_shields/cookie_pref_service_factory.cc",
    "huhi_shields/cookie_pref_service_factory.h",
    "huhi_tab_helpers.cc",
    "huhi_tab_helpers.h",
    "browser_context_keyed_service_factories.cc",
    "browser_context_keyed_service_factories.h",
    "component_updater/huhi_component_installer.cc",
    "component_updater/huhi_component_installer.h",
    "component_updater/huhi_component_updater_configurator.cc",
    "component_updater/huhi_component_updater_configurator.h",
    "component_updater/huhi_component_updater_delegate.cc",
    "component_updater/huhi_component_updater_delegate.h",
    "geolocation/huhi_geolocation_permission_context_delegate.cc",
    "geolocation/huhi_geolocation_permission_context_delegate.h",
    "metrics/metrics_reporting_util.cc",
    "metrics/metrics_reporting_util.h",
    "search_engines/guest_window_search_engine_provider_service.cc",
    "search_engines/guest_window_search_engine_provider_service.h",
    "search_engines/private_window_search_engine_provider_service.cc",
    "search_engines/private_window_search_engine_provider_service.h",
    "search_engines/search_engine_provider_service.cc",
    "search_engines/search_engine_provider_service.h",
    "search_engines/search_engine_provider_service_factory.cc",
    "search_engines/search_engine_provider_service_factory.h",
    "search_engines/search_engine_provider_util.cc",
    "search_engines/search_engine_provider_util.h",
    "search_engines/search_engine_tracker.cc",
    "search_engines/search_engine_tracker.h",
    "search_engines/tor_window_search_engine_provider_service.cc",
    "search_engines/tor_window_search_engine_provider_service.h",
    "sync/huhi_profile_sync_service_delegate.cc",
    "sync/huhi_profile_sync_service_delegate.h",
    "update_util.cc",
    "update_util.h",
  ]

  if (is_mac) {
    sources += [
      "huhi_browser_main_parts_mac.h",
      "huhi_browser_main_parts_mac.mm",
    ]
  }

  if (enable_sparkle) {
    sources += [
      "mac/sparkle_glue.h",
      "mac/sparkle_glue.mm",
      "mac/su_updater.h",
    ]
  }

  deps = [
    ":sparkle_buildflags",
    ":version_info",
    "autoplay",
    "huhi_stats:stats_updater",
    "browsing_data",
    "content_settings",
    "download",
    "ephemeral_storage",
    "farbling",
    "net",
    "ntp_background_images",
    "profiles",
    "renderer_context_menu",
    "search",
    "themes",
    "ui",
    "//base",
    "//huhi/browser/net",
    "//huhi/browser/notifications",
    "//huhi/browser/translate/buildflags",
    "//huhi/common",
    "//huhi/common:pref_names",
    "//huhi/components/binance/browser/buildflags",
    "//huhi/components/huhi_component_updater/browser",
    "//huhi/components/huhi_drm",
    "//huhi/components/huhi_referrals/buildflags",
    "//huhi/components/huhi_shields/browser",
    "//huhi/components/huhi_shields/common",
    "//huhi/components/huhi_sync",
    "//huhi/components/huhi_together/browser",
    "//huhi/components/huhi_wallet/buildflags",
    "//huhi/components/huhi_wayback_machine:buildflags",
    "//huhi/components/huhi_webtorrent/browser/buildflags",
    "//huhi/components/content_settings/core/browser",
    "//huhi/components/crypto_dot_com/browser/buildflags",
    "//huhi/components/gemini/browser/buildflags",
    "//huhi/components/greaselion/browser/buildflags",
    "//huhi/components/ipfs/buildflags",
    "//huhi/components/l10n/browser",
    "//huhi/components/moonpay/browser/buildflags",
    "//huhi/components/ntp_background_images/browser",
    "//huhi/components/ntp_tiles",
    "//huhi/components/p3a",
    "//huhi/components/resources",
    "//huhi/components/speedreader:buildflags",
    "//huhi/components/tor/buildflags",
    "//huhi/components/weekly_storage",
    "//huhi/services/network/public/cpp",
    "//chrome/common",
    "//components/autofill/core/common",
    "//components/browsing_data/core",
    "//components/component_updater",
    "//components/content_settings/browser",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/embedder_support",
    "//components/gcm_driver:gcm_buildflags",
    "//components/gcm_driver:gcm_driver",
    "//components/keyed_service/content",
    "//components/metrics",
    "//components/password_manager/core/common",
    "//components/permissions",
    "//components/policy/core/browser",
    "//components/prefs",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/search_engines",
    "//components/services/patch/content",
    "//components/services/unzip/content",
    "//components/sessions",
    "//components/sync_device_info",
    "//components/spellcheck/browser",
    "//components/unified_consent",
    "//components/update_client:patch_impl",
    "//components/update_client:unzip_impl",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/buildflags",
    "//services/network/public/cpp",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/widevine/cdm:buildflags",
    "//ui/base",
  ]

  if (huhi_wallet_enabled) {
    deps += [
      "//huhi/components/huhi_wallet",
      "huhi_wallet",
    ]
  }

  if (enable_greaselion) {
    deps += [
      "greaselion",
      "//huhi/components/greaselion/browser",
    ]
  }

  if (enable_huhi_sync) {
    deps += [ "//huhi/components/huhi_sync" ]
  }

  if (!is_android) {
    sources += [
      "p3a/p3a_core_metrics.cc",
      "p3a/p3a_core_metrics.h",
    ]

    deps += [
      "importer",
      "infobars",
      "//huhi/app:huhi_generated_resources_grit",
    ]
  } else {
    deps += [ "android:android_browser_process" ]
  }

  if (binance_enabled) {
    sources += [
      "binance/binance_protocol_handler.cc",
      "binance/binance_protocol_handler.h",
      "binance/binance_service_factory.cc",
      "binance/binance_service_factory.h",
    ]

    deps += [
      "//huhi/components/binance/browser",
      "//components/country_codes",
    ]

    if (is_android) {
      sources += [
        "binance/android/binance_native_worker.cc",
        "binance/android/binance_native_worker.h",
      ]
    }
  }

  if (gemini_enabled) {
    sources += [
      "gemini/gemini_protocol_handler.cc",
      "gemini/gemini_protocol_handler.h",
      "gemini/gemini_service_factory.cc",
      "gemini/gemini_service_factory.h",
    ]

    deps += [
      "//base",
      "//huhi/browser/profiles",
      "//huhi/common",
      "//huhi/components/gemini/browser",
      "//components/country_codes",
      "//components/keyed_service/content",
      "//components/user_prefs",
      "//content/public/browser",
      "//ui/base",
      "//url",
    ]
  }

  if (crypto_dot_com_enabled) {
    sources += [
      "crypto_dot_com/crypto_dot_com_service_factory.cc",
      "crypto_dot_com/crypto_dot_com_service_factory.h",
    ]

    deps += [
      "//base",
      "//huhi/browser/profiles",
      "//huhi/components/crypto_dot_com/browser",
      "//huhi/components/crypto_dot_com/common",
      "//components/keyed_service/content",
    ]
  }

  if (ipfs_enabled) {
    sources += [
      "ipfs/content_browser_client_helper.cc",
      "ipfs/content_browser_client_helper.h",
      "ipfs/ipfs_service_factory.cc",
      "ipfs/ipfs_service_factory.h",
      "ipfs/ipfs_tab_helper.cc",
      "ipfs/ipfs_tab_helper.h",
    ]

    deps += [
      "//huhi/browser/infobars",
      "//huhi/components/ipfs",
      "//huhi/components/services/ipfs/public/mojom",
      "//components/user_prefs",
      "//extensions/browser",
      "//extensions/common",
    ]
  }

  if (huhi_together_enabled) {
    deps += [ "//huhi/components/huhi_together/browser" ]
  }

  if (moonpay_enabled) {
    deps += [
      "//huhi/components/moonpay/browser",
      "//huhi/components/moonpay/common",
    ]
  }

  if (enable_tor) {
    deps += [
      "//huhi/browser/tor",
      "//huhi/components/tor",
    ]
  }

  if (enable_extensions) {
    deps += [
      "extensions",
      "//extensions/common:common_constants",
    ]
  }

  if (enable_huhi_referrals) {
    sources += [
      "huhi_referrals/huhi_referrals_service_factory.cc",
      "huhi_referrals/huhi_referrals_service_factory.h",
    ]
    deps += [
      "//huhi/components/huhi_referrals/browser",
      "//huhi/components/huhi_referrals/common",
      "//huhi/components/huhi_stats/browser",
    ]
  }

  if (huhi_ads_enabled) {
    deps += [
      "//huhi/components/huhi_ads/browser",
      "//huhi/components/huhi_user_model/browser",
      "//components/dom_distiller/core",
    ]
  }

  if (huhi_rewards_enabled) {
    sources += [
      "huhi_rewards/rewards_service_factory.cc",
      "huhi_rewards/rewards_service_factory.h",
      "huhi_rewards/rewards_tab_helper.cc",
      "huhi_rewards/rewards_tab_helper.h",
    ]

    if (enable_extensions) {
      sources += [
        "huhi_rewards/extension_rewards_notification_service_observer.cc",
        "huhi_rewards/extension_rewards_notification_service_observer.h",
        "huhi_rewards/extension_rewards_service_observer.cc",
        "huhi_rewards/extension_rewards_service_observer.h",
      ]
    }

    if (is_android) {
      sources += [
        "huhi_rewards/android/huhi_rewards_native_worker.cc",
        "huhi_rewards/android/huhi_rewards_native_worker.h",
      ]
    } else {
      sources += [
        "huhi_rewards/tip_dialog.cc",
        "huhi_rewards/tip_dialog.h",
      ]
    }

    deps += [
      "//huhi/components/huhi_rewards/browser",
      "//huhi/vendor/bat-native-ledger:headers",
    ]
  }

  if (enable_huhi_webtorrent) {
    deps += [ "//huhi/components/huhi_webtorrent/browser" ]
  }

  if (enable_huhi_wayback_machine) {
    deps += [ "//huhi/components/huhi_wayback_machine" ]
  }

  if (enable_widevine) {
    sources += [
      "huhi_drm_tab_helper.cc",
      "huhi_drm_tab_helper.h",
    ]
    deps += [ "//huhi/browser/widevine" ]
  }

  if (enable_huhi_perf_predictor) {
    deps += [ "//huhi/components/huhi_perf_predictor/browser" ]
  }

  if (enable_speedreader) {
    sources += [
      "//huhi/browser/speedreader/speedreader_service_factory.cc",
      "//huhi/browser/speedreader/speedreader_service_factory.h",
      "//huhi/browser/speedreader/speedreader_tab_helper.cc",
      "//huhi/browser/speedreader/speedreader_tab_helper.h",
    ]
    deps += [ "//huhi/components/speedreader" ]
  }

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/google/google_update_win.cc",
      "//chrome/browser/google/google_update_win.h",
      "//chrome/browser/google/switches.cc",
      "//chrome/browser/google/switches.h",
    ]

    deps += [ "//google_update" ]
  }
}

buildflag_header("sparkle_buildflags") {
  header = "sparkle_buildflags.h"
  flags = [ "ENABLE_SPARKLE=$enable_sparkle" ]
}

source_set("version_info") {
  # these are only for version_info.cc and should not be exposed as public configs
  configs += [ "//huhi/build:version" ]
  defines = [ "HUHI_BROWSER_VERSION=\"$huhi_version\"" ]

  sources = [
    "version_info.cc",
    "version_info.h",
  ]
}

source_set("browser") {
  visibility = [
    "//chrome/browser/*",
    "//huhi/browser/*",
    "//huhi:browser_dependencies",
    "//huhi/test:*",
  ]

  deps = [ "//chrome/browser" ]
  if (enable_sparkle) {
    deps += [ "//huhi/vendor/sparkle:sparkle_framework_bundle_data" ]
  }
}
