import("//build/util/branding.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("channel_info") {
  if (is_linux) {
    sources = [
      "huhi_channel_info_posix.cc",
      "huhi_channel_info_posix.h",
    ]

    deps = [
      "//base:base",
      "//base:logging_buildflags",
      "//components/version_info:version_info",
    ]

    configs += [
      "//huhi/build/linux:linux_channel_names",
    ]
  }
}

source_set("network_constants") {
  sources = [
    "network_constants.cc",
    "network_constants.h",
  ]
}

source_set("pref_names") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [ "//components/gcm_driver:gcm_buildflags" ]
}

source_set("switches") {
  sources = [
    "huhi_switches.cc",
    "huhi_switches.h",
  ]

  deps = [
    "//base",
  ]
}

source_set("common") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10653 is resolved
  check_includes = false
  deps = []

  sources = [
    "huhi_channel_info.cc",
    "huhi_channel_info.h",
    "huhi_features.cc",
    "huhi_features.h",
    "huhi_paths.cc",
    "huhi_paths.h",
    "common_message_generator.cc",
    "common_message_generator.h",
    "resource_bundle_helper.cc",
    "resource_bundle_helper.h",
    "translate_network_constants.cc",
    "translate_network_constants.h",
    "url_constants.cc",
    "url_constants.h",
    "webui_url_constants.cc",
    "webui_url_constants.h",
  ]

  if (!is_android && !is_ios) {
    deps += [
      "//huhi/common/importer",
    ]
  }

  if (enable_extensions) {
    sources += [
      "extensions/huhi_extensions_api_provider.cc",
      "extensions/huhi_extensions_api_provider.h",
      "extensions/whitelist.cc",
      "extensions/whitelist.h",
    ]

    deps += [ "//extensions/common:common_constants" ]

    public_deps = [
      "extensions/api",
    ]
  }

  deps += [
    "//base",
    ":network_constants",
    ":pref_names",
    ":shield_exceptions",
    ":switches",
    "//huhi/chromium_src:common",
    "//huhi/components/resources",
    "//components/resources",
    "//extensions/buildflags",
    "//services/service_manager",
    "//ui/base",
  ]

  if (!is_ios) {
    sources += [
      "huhi_content_client.cc",
      "huhi_content_client.h",
    ]

    deps += [
      "//content/public/common",
    ]
  }
}

source_set("shield_exceptions") {
  # Remove when https://github.com/huhisoft/huhi-browser/issues/10653 is resolved
  check_includes = false
  sources = [
    "shield_exceptions.cc",
    "shield_exceptions.h",
  ]

  deps = [
    "//huhi/extensions:common",
    "//url",
  ]
}

config("constants_configs") {
  defines = []
  if (is_mac) {
    defines += [
      "HUHI_PRODUCT_STRING=\"$chrome_product_full_name\"",
    ]
  }
}
